import java.text.SimpleDateFormat

/*
 * Copyright (Â©) 2016 Jeff Harris <jefftharris@gmail.com>
 * All rights reserved. Use of the code is allowed under the
 * Artistic License 2.0 terms, as specified in the LICENSE file
 * distributed with this code, or available from
 * http://www.opensource.org/licenses/artistic-license-2.0.php
 */

apply plugin: 'com.android.application'

static def gitRevision() {
    def cmd = "git rev-parse --short HEAD"
    return cmd.execute().text.trim()
}

static def buildTime() {
    def df = new SimpleDateFormat("MM/dd/yyyy HH:mm:ss Z")
    return df.format(new Date())
}

android {
    namespace 'net.tjado.passwdsafe'

    compileSdk 34
    defaultConfig {
        applicationId 'net.tjado.passwdsafe'
        minSdkVersion 21
        resourceConfigurations += ['de']
        targetSdkVersion 34
        versionCode 500
        versionName '0.5.0'

        buildConfigField 'String', 'BUILD_ID', "\"${gitRevision()}\""
        buildConfigField 'String', 'BUILD_DATE', "\"${buildTime()}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //Reference them in .xml files.
        resValue "string", "application_id", "${applicationId}"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString(),
                             "room.incremental":"true"]
            }
        }
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles += getDefaultProguardFile(
                    'proguard-android-optimize.txt')
            proguardFiles += 'proguard-rules.pro'
            proguardFiles += 'proguard-rules-debug.pro'
            testProguardFiles += getDefaultProguardFile(
                    'proguard-android-optimize.txt')

            ndk {
                debugSymbolLevel "FULL"
            }
        }
        release {
            minifyEnabled true
            proguardFiles += getDefaultProguardFile(
                    'proguard-android-optimize.txt')
            proguardFiles += 'proguard-rules.pro'

            ndk {
                debugSymbolLevel "FULL"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    productFlavors {
    }
    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
    ndkVersion = '21.3.6528147'

    packagingOptions {
        jniLibs {
            useLegacyPackaging false
        }
    }
    testOptions {
        animationsDisabled true
    }
}

if (project.file('../sign/sign.gradle').exists()) {
    apply from: '../sign/sign.gradle'
}

dependencies {
    def room_version = "2.5.2"
    def yubikey_version = "2.3.0"
    def espresso_version = "3.5.1"

    api 'androidx.annotation:annotation:1.7.0'
    api 'androidx.cardview:cardview:1.0.0'
    api 'androidx.constraintlayout:constraintlayout:2.1.4'
    api 'androidx.gridlayout:gridlayout:1.0.0'
    api 'androidx.legacy:legacy-preference-v14:1.0.0'
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'androidx.lifecycle:lifecycle-viewmodel:2.6.2'
    api 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
    api 'androidx.preference:preference:1.2.1'
    api 'com.google.android.material:material:1.9.0'

    // Avoiding duplicate class errors
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.22"))

    implementation 'androidx.appcompat:appcompat:1.7.0-alpha03'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'

    implementation "com.yubico.yubikit:yubiotp:$yubikey_version"
    implementation "com.yubico.yubikit:android:$yubikey_version"
    implementation 'com.github.tony19:logback-android:3.0.0'

    implementation 'com.github.bmelnychuk:atv:1.2.9'
    implementation 'com.mikepenz:iconics-core:2.8.1@aar'
    implementation 'com.mikepenz:material-design-iconic-typeface:2.2.0.2@aar'
    implementation 'com.mikepenz:devicon-typeface:2.0.0.2@aar'
    implementation 'com.mikepenz:materialize:1.0.0@aar'
    implementation 'com.mikepenz:fastadapter:2.0.0@aar'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.71.1'
    implementation 'org.bouncycastle:bcpg-jdk18on:1.71.1'
    implementation 'io.fotoapparat.fotoapparat:library:1.4.1'
    implementation 'com.google.zxing:core:3.5.1'

    // FIDO (WebAuthn/U2F) dependencies
    implementation 'co.nstant.in:cbor:0.9'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.google.guava:guava:31.1-android'
    implementation 'rocks.xmpp:precis:1.1.0'

    implementation "androidx.room:room-runtime:$room_version"

    annotationProcessor "androidx.room:room-compiler:$room_version"

    // Core library
    androidTestImplementation 'androidx.test:core:1.5.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.ext:truth:1.5.0'
    androidTestImplementation 'com.google.truth:truth:1.1.3'

    // Espresso dependencies
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-accessibility:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-web:$espresso_version"
    androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:$espresso_version"

    // The following Espresso dependency can be either "implementation"
    // or "androidTestImplementation", depending on whether you want the
    // dependency to appear on your APK's compile classpath or the test APK
    // classpath.
    androidTestImplementation "androidx.test.espresso:espresso-idling-resource:$espresso_version"
}
